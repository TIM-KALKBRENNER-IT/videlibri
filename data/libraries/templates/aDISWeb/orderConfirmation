<t:switch prioritized="true">
  <form>
    <t:switch-prioritized>
      <input title="Zurück zur vorherigen Seite">{backButtonName := uri-encode(@name)}</input>
      <input t:condition="contains(@title, 'zur vorherigen Seite')">{backButtonName := uri-encode(@name)}</input>
    </t:switch-prioritized> 
    {form := form(., (
      {"select": "_DESTINATIONFILLER_"},
      (.//select[@name = "select$0"])[1]/{@name: "Nein"}, (: benachrichtigung :)
      (.//input[@type = "submit"])[1]
      )),
     goBackPage := form(., x"{$backButtonName}.x=0&{$backButtonName}.y=0")
    }
    
    <DIV class="fsgrp">
      <DIV t:condition="@id=('OPACLI','R03')"> <!-- OPACLI is the old value, not used anymore anywhere?-->
        <STRONG>{tempOrderConfirmation := x"Wohin soll das Buch ""{.}"" vorbestellt werden?"}</STRONG>
      </DIV>
  
      <select name="select">{tempOptions :=  ./option[. != ""],    
      vl:choose("internal-order-confirmed", $tempOrderConfirmation, $tempOptions, $tempOptions/@value)
      }</select>    
    </DIV>
  </form>

  <form> 
    {tempOrderConfirmation := x"Wohin soll das Buch ""{css("h2, span.titanzeige")}"" vorbestellt werden?"}
    <select name="select" t:condition="not(starts-with(@id,'SUC'))">{tempOptions :=  ./option[. != ""],    
    vl:choose("internal-order-confirmed", $tempOrderConfirmation, $tempOptions, $tempOptions/@value)
    }</select>
    <input type="submit"/>
    <input type="submit" t:condition="(@value,@class)=('Abbrechen','Zurück')">{$cancelinput}</input>
    {form := form(., (
      {"select": "_DESTINATIONFILLER_"},
      (.//select[@name = "select$0"])[1]/{@name: "Nein"}, (: benachrichtigung :)
      (.//input[@type = "submit"])[1]
      )),
     goBackPage := form(., $cancelinput) )
  </form>

  <form><div class="message"><div id="R01">
    <!--<h1>Bitte auf Band oder Heft verzweigen und dann bestellen!</h1>-->
    <h1>Bitte auf den gewünschten Bestand oder Band verzweigen</h1>   
  </div></div>{subBookOnly:=.}</form>

  
  <form><div class="message"><div id="R01">
    <h1>{error:=.}</h1>
  </div></div>{goBackPage:=.}</form>

  
  <input name="$Textfield">{ 
    $internal-loggedin := false(), 
    loginFormPost := 
      let $form := (ancestor::form, //form)[1] (:Aalen has invalid html where the form is not an ancestor of the input:)
      let $buttons := $form//input[@type="submit" and matches(@value, "Anmeld|Login")]
      let $buttons := if ($buttons) then $buttons else "textButton=Anmeldung abschicken" (:vl:raise-internal("Anmeldebutton nicht gefunden"):)
      return form($form, ({"$Textfield": $username, "$Textfield$0": $password}, $buttons[1])) 
  }</input> 
  <input name="j_username">{ $internal-loggedin := false(), loginFormPost := form((ancestor::form, //form)[1], {"j_username": $username, "j_password": $password}), idpSystem := true() }</input> 

  <body class="msgpage">{error := inner-text()}</body>
</t:switch>
   