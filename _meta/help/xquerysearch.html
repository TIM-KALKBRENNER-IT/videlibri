<html>
<head>
<meta name="MS-HKWD" content="XQuery">
<meta name="MS-HKWD" content="Suche">
<meta name="MS-HKWD" content="Ausleihen">
<meta name="MS-HKWD" content="Historie">
<link rel="stylesheet" href="style.css">
<style>pre { margin-bottom: 2em, padding-left: 1em }</style>
<title>XQuery-Suche</title>
</head>
<body>
<div class="main">
<h1>XQuery-Suche</h1>
<p>
Mittels XQuery kann man beliebig komplizierte Suchanfragen an die eigene Ausleihenhistorie stellen. 
In der Desktopversion kann man diese Suchanfragen über den Dialog vom Menü "Extras\XQuery Suche..." eingeben, in der Androidversion kann man die Anfrage direkt im Suchfeld unterhalb der Ausleihenliste eingeben.<br/><br>
<!--<br><img src="bibtexexport.gif" style="margin:1em"><br>-->

Wie der Titel sagt, werden diese Abfragen in <a href="https://de.wikipedia.org/wiki/XQuery">XQuery</a> formuliert, was zu viele Möglichkeiten bietet, um sie hier alle zu beschreiben, deshalb werden im Folgenden nur einige mögliche Beispielabfragen aufgelistet. Wer die weiteren Möglichkeiten von XQuery kennenlernen will, kann sich in der Bibliothek ein Lehrbuch über XQuery ausleihen. Zusätzlich zu XQuery sollte man wissen, dass VideLibri  eine Variable <code>$books</code> bereitstellt, die alle Bücher enthält. Die für jedes Buch jeweils vorhandenen Eigenschaften sind auf der Hilfeseite zum <a href="seiten.html">Auslesen von Bücher-enthaltenen Webseiten</a> beschrieben.


<p>Suche alle jemals ausgeliehenen Bücher eines bestimmten Autors (z.B.: von Meyer):

<pre>
for $book in $books 
where $book.author = "Meyer"
return $book
</pre> 

<p>Suche alle Bücher über ein bestimmtes Thema (z.B.: über Schatten), sortiert nach Autor:

<pre>
for $book in $books 
where contains($book.title, "schatten")
order by $book.author
return $book
</pre>

<p>Suche alle jemals ausgeliehenen Bücher von mehreren bestimmten Autoren (z.B.: von Meyer und Pratchett):

<pre>
for $book in $books 
where $book.author = ("Meyer", "Pratchett")
return $book
</pre> 


<p>Suche alle jemals ausgeliehenen Bücher von mehreren bestimmten Autoren (z.B.: von Meyer und Pratchett) (Kurzversion, ohne Sortiermöglichkeit und beliebige Vornamen):

<pre>
$books[matches(author, "Meyer|Pratchett")]
</pre> 


<p>Sortiere alle Bücher nach der Länge des Titels:

<pre>
for $book in $books
order by string-length($book.title)
return $book
</pre>

<p>Ermittel die Zahl aller jemals ausgeliehenen Bücher:

<pre>
count($books)</pre>

<p>Finde alle Autoren, von denen mehr als ein Buch ausgeliehen worden ist:

<pre>
for $book in $books 
group by $author := $book.author
where count($book) > 1
return $author
</pre>

<p>Finde alle Bücher, die mehrmals ausgeliehen worden sind:

<pre>
for $book in $books 
group by $temp := $book.author || ":" || $book.title
where count($book) > 1
return $book
</pre>

<p>Finde alle Bücher, die mehrmals ausgeliehen worden sind, aber zeige jedes Buch nur einmal an:

<pre>
for $book in $books 
group by $author := $book.author || ":" || $book.title
where count($book) > 1
return $book[1]
</pre>

<p>Ermittle für jeden Autor, wie viele Bücher von ihm ausgeliehen worden sind:

<pre>
for $book in $books 
group by $author := $book.author
order by count($book)
return {"author": $author, "title":  count($book)}
</pre>

<p>Finde alle Bücher, deren Titel aus mindestens 5 Wörtern besteht:

<pre>
for $book in $books
where matches($book.title, "(\w+(\W+|$)){5}")
return $book
</pre>


<p>Finde alle Bücher, bei denen der Autorname mindestens 3 Vokale und der Titel mindestens 17 Konsonenten enthält:

<pre>
for $book in $books
where matches($book.author, "([aeiou].*){3}", "i") and matches(replace($book.title,"[^a-z]","","i"), "([^aeiou].*){17}", "i")
return $book
</pre>


<p>Berechne die Summe aller Zahlen von 1 bis 100: (in der Androidversion muss xquery version "3.0"; vorangestellt werden, wenn $books nicht im Ausdruck vorkommt)

<pre>
sum(1 to 100)
</pre>

<p>Suche alle Primzahlen unter 100:

<pre>
for $i in 2 to 100
where empty((2 to $i - 1)[$i mod . = 0])
return $i
</pre>

<p>Finde alle Bücher, deren Titellänge eine Primzahl ist:

<pre>
let $primes := (for $i in 2 to 200
  where empty((2 to $i - 1)[$i mod . = 0])
  return $i)
return $books[string-length(title) = $primes]
</pre>

<p>Finde alle Bücher, deren Titellänge eine Primzahl ist, wobei nur Buchstaben (oder Zahlen, etc.) gezählt werden und Sonderzeichen (z.B.: !) ignoriert werden:

<pre>
let $primes := (for $i in 2 to 200
  where empty((2 to $i - 1)[$i mod . = 0])
  return $i)
return $books[string-length(replace(title, "\W", "")) = $primes]
</pre>


<p>Ermittle das aktuelle Datum und Uhrzeit:

<pre>
current-date(), current-time()
</pre>

<p>Finde alle Bücher, deren Titel den aktuellen Monatsnamen enthält:

<pre>
let $month := format-date(current-date(), "[Mn]", "de", (), ())
return $books[contains(title, $month)]
</pre>

<p>Finde alle Bücher, deren Titel <i>irgendeinen</i> Monatsnamen enthält:

<pre>
let $months := (1 to 12) ! format-date(xs:date("2010-"||format-integer(.,"00")||"-01"), "[Mn]", "de", (), ())
return $books[some $month in $months satisfies contains(title, $month)]
</pre>

(Übung für den Leser: was muss man daraus löschen, um nur eine Liste der Monate auszugeben?)


<p>Verschlüssele einen Text mittels Cäsarchiffre:

<pre>
let $a := x:cps("a") return string-join( x:cps(x:cps("dertext") ! ((. - $a + 3) mod 26 + $a) ) )
</pre>

<p>Entschlüssele einen Text mittels Cäsarchiffre:

<pre>
let $a := x:cps("a") return string-join( x:cps(x:cps("ghuwhaw") ! ((. - $a + 23) mod 26 + $a) ) )
</pre>

<p>Erstelle 100 virtuelle Bücher, 1 vom Verfasser 1, 4 von 2, 9 von 3, ..:

<pre>
(1 to 100) ! {"author": ., "title": . * .}
</pre>

<p>Erstelle 1000 zufällige Bücher, indem  vorhandenen Autornamen und Titelwörter zufällig zusammengemixt werden:

<pre>
let $names := $books!tokenize(author, "\W+")[.]
let $titles := $books!tokenize(title, "\W+")[.]
return (random-seed(), (1 to 1000) ! {
  "author": let $temp := random(count($names))+1 return $names[$temp], 
  "title": join((1 to (random(7)+1))!(let $temp := random(count($titles))+1 return $titles[$temp]))})
</pre>
 
<p>Berechne die Fakultäten von 1 bis 100: "eins", "zwei", "sechs" bis "dreiundneunzig Sesvigintillionen..." </p>
 
<pre>
(1 to 100) ! (x:product(1 to .)) ! format-integer(., "w", "de")
</pre>
 <!--
<p>Berechne fizzbuzz, die Zahlen von 1 bis 100 und Vielfache von drei durch fizz und Vielfache von 5 durch buzz ersetzt:</p>
 
<pre>
for $i in 1 to 100 return (("fizz"[$i mod 3=0] || "buzz"[$i mod 5=0])[.],$i)[1]
</pre>-->

</body>
</div>
</html>
